#define WIN_10

import "../preamble.prag"
import "handmade_interface.prag"
import "../wasapi/wasapi.prag"


mod Handmade::Platform
{
	with Windows;

	// https://github.com/ecxr/handmadehero/blob/master/code/win32_handmade.cpp

	// TODO: 
	// - Saved game locations
	// - Getting a handle to our own executable file
	// - Asset loading pppath
	// - Threadingawsad
	// - Raw input
	// - Sleep/timeBeginPeriod
	// - ClipCursor for multimonitor support
	// - Fullscreen support
	// - WM_SETCURSOR (cursor visibility)
	// - QueryCancelAutoplay
	// - WM_ACTIATEAPP (when we are not active app)
	// - Blit speed improvements (bitblit)
	// - Hardware accelereration
	// - GetKeyboardLayout (for internationalization)


	let HANDMADE_INTERAL = true;
	let DEBUG_DRAW_STATS = true;

	let offscreen_buffer = struct
	(
		info:   BITMAPINFO;
		memory: ptr;
		width:  i32;
		height: i32;
		pitch:  i32;
	);
	let window_info = struct 
	(
		handle: mm;
		dc: mm;
		width: i32;
		height: i32;
	);

	let win_state = struct
	(
		game_memory: game_memory*;
		is_recording: bool;
		is_replaying: bool;
		recording_started: bool;
		replaying_started: bool;
		recording_file_handle: mm;
		replaying_file_handle: mm;
	);

	var backbuffer: offscreen_buffer;
	var window: window_info;
	var window_requests_quit = false;

	// let platform_write_file = fun (name: string; buffer: i8[]) => bool;
	let platform_write_file: platform_write_file = 
	{
		var handle = CreateFileA(cstr(name), GENERIC_WRITE, 0, nullptr, CREATE_ALWAYS, 0, 0);
		if (handle == null) {
			CloseHandle(handle);
			return false;
		}
		var bytes_written: i32 = 0;
		var result = WriteFile(handle, buffer.data, buffer.length, &bytes_written, nullptr);
		if (result != 0) {
			CloseHandle(handle);
			assert(buffer.length == bytes_written);
			return true;
		} else {
			CloseHandle(handle);
			return false;
		}

		CloseHandle(handle);
		return false;
	}

	// let platform_read_file = fun (name: string) => i8[];
	let platform_read_file: platform_read_file =  
	{
		var result: i8[];
		result.data = nullptr;
		result.length = 0;
		var handle = CreateFileA(cstr(name), GENERIC_READ, FILE_SHARE_READ, nullptr, OPEN_EXISTING, 0, 0);
		if (handle == null) {
			return result;
		}

		var size: i64 = 0;
		var fsr = GetFileSizeEx(handle, &size);
		if (fsr == 0 || size == 0) {
			CloseHandle(handle);
			return result;
		}

		var buffer = VirtualAlloc(nullptr, size@mm, MEM_COMMIT, PAGE_READWRITE);
		if (buffer@mm == null) {
			CloseHandle(handle);
			return result;
		}
		assert(size@mm <=\ gigabytes(4));
		var size_32 = @i32 size;
		var bytes_read: i32;
		var rfr = ReadFile(handle, buffer, size_32, &bytes_read,  nullptr);
		assert(bytes_read == size_32);
		if (rfr == 0) {
			VirtualFree(buffer, null, MEM_RELEASE);
			CloseHandle(handle);
			return result;		
		}

		result.data = buffer;
		result.length = size_32;
		CloseHandle(handle);
		return result;
	} 

	let platform_free_file_memory: platform_free_file_memory =  
	{
		VirtualFree(mem, null, MEM_RELEASE);
	}

	let game_interface = struct
	(
		library: mm;
		update_and_render: game_update_and_render;
		output_sound: game_output_sound;
		is_loaded: bool;
		dll_last_write_time: i64;
	);

	let refresh_game_dll = fun
	(
		game: game_interface*;
	) => void
	{
		var current_write_time = get_last_write_time("handmade.dll\0");
		if (current_write_time != game.dll_last_write_time) {
			unload_game_dll(game);
			*game = load_game_dll();
		}
	}

	let load_game_dll = fun () => game_interface
	{
		var source_str = "handmade.dll\0";
		var temp_str = "handmade_temp.dll\0";
		var result = game_interface{};

		result.dll_last_write_time = get_last_write_time(source_str);
		var copy_result = CopyFileA(cstr(source_str), cstr(temp_str), 0);
		
		result.library = LoadLibraryA(cstr(temp_str));
		result.update_and_render = @game_update_and_render GetProcAddress(result.library, cstr("game_update_and_render\0"));
		assert(result.update_and_render@ptr != nullptr);
		result.output_sound = @game_output_sound GetProcAddress(result.library, cstr("game_output_sound\0"));
		assert(result.output_sound@ptr != nullptr);
		result.is_loaded = true;

		print("load library\n");
		return result;
	}

	let unload_game_dll = fun (@interface: game_interface*) => void
	{
		if (library != null) {
			var result = FreeLibrary(library);
			assert(result != 0);
		}
		update_and_render = @game_update_and_render nullptr;
		output_sound = @game_output_sound nullptr;
		is_loaded = false;
		library = null;
		print("unload library\n");
	}

	let get_last_write_time = fun
	(
		filename: string;
	) => i64
	{
		var cs = cstr(filename);
		var data: WIN32_FILE_ATTRIBUTE_DATA;
		GetFileAttributesExA(cstr("handmade.dll\0"), GetFileExInfoStandard, &data);
		return data.LastWriteTime;
	}

	let create_backbuffer = fun (@buffer: offscreen_buffer*; target_width: i32; target_height: i32) => void 
	{
		let bytes_per_pixel = 4;
		width = target_width;
		height = target_height;
		info.header.biSize = 40;
		info.header.biWidth = width;
		info.header.biHeight = -height;
		info.header.biPlanes = 1;
		info.header.biBitCount = 32;
		info.header.biCompression = BI_RGB;
		var bitmap_size = bytes_per_pixel * width * height;
		if (memory != nullptr) {
			VirtualFree(buffer.memory, null, MEM_RELEASE);
		}
		memory = VirtualAlloc(nullptr, bitmap_size@\mm, MEM_COMMIT, PAGE_READWRITE);
		pitch = buffer.width * bytes_per_pixel;
	}

	let blit_to_screen = fun (buffer: offscreen_buffer*) => void 
	{
		StretchDIBits(window.dc,
			0, 0, buffer.width, buffer.height,
			0, 0, buffer.width, buffer.height,
			buffer.memory, &buffer.info,
			DIB_RGB_COLORS, SRCCOPY
			);
	}

	let update_window_rect = fun () => void 
	{
		var rect: RECT;
		GetClientRect(window.handle, &rect); 
		window.width = rect.right - rect.left;
		window.height = rect.bottom - rect.top;
	}

	let main_window_callback = fun 
	(
		window_handle: mm;
		message: i32;
		w_param: mm;
		l_param: mm;
	) => mm 
	{
		var result = null;
		if (message == WM_SIZE) {
			update_window_rect();	
		}
		elif (message == WM_CLOSE) {
			window_requests_quit = true;
		}
		elif (message == WM_DESTROY) {
			window_requests_quit = true;
		}
		elif (message == WM_PAINT) {
			var paint: PAINTSTRUCT;
			var context = BeginPaint(window_handle, &paint);
			blit_to_screen(&backbuffer);
			EndPaint(window_handle, &paint);
		} 
		else {
			result = DefWindowProcA(window_handle, message, w_param, l_param);
		} 
		return result;
	}


	let update_game_button = fun (button: game_button*; is_pressed: bool) => void 
	{
		if (is_pressed && !button.pressed) {
			button.down = true;
		}
		if (!is_pressed && button.pressed) {
			button.up = true;
		}
		button.pressed = is_pressed;
	}

	let get_mouse_input = fun (game_input: game_input*) => void 
	{
		var cursor_pos: POINT;
		GetCursorPos(&cursor_pos);
		ScreenToClient(window.handle, &cursor_pos);
		game_input.mouse_x = cursor_pos.x;
		game_input.mouse_y = cursor_pos.y;
		update_game_button(&game_input.left_mb, (GetKeyState(VK_LBUTTON) & @i16 (1 << 15)) != 0);
		update_game_button(&game_input.right_mb, (GetKeyState(VK_RBUTTON) & @i16 (1 << 15)) != 0);
		update_game_button(&game_input.middle_mb, (GetKeyState(VK_MBUTTON) & @i16 (1 << 15)) != 0);
	}

	let process_pending_messages = fun 
	(
		game_input: game_input*;
		win_state: win_state*;
	) => void 
	{
		var msg: MSG;
		while (PeekMessageA(&msg, null, 0, 0, PM_REMOVE) != 0) {
			var message = msg.message;
			if (message == WM_QUIT) {
				game_input.request_quit = true;
			} 
			elif (message == WM_SYSKEYDOWN
				|| message == WM_SYSKEYUP
				|| message == WM_KEYDOWN
				|| message == WM_KEYUP) {

				var w_param = @i32 (msg.wParam >> 32@mm);
				var l_param = @i32 (msg.lParam >> 32@mm);
				var vk_code = w_param;

				var was_down = (l_param & (1 << 30)) != 0;
				var is_down = (l_param & (1 << 31)) == 0;
				var key_up = was_down && !is_down;
				var key_down = !was_down && is_down; 

				if(vk_code == VK_ESCAPE) {
					game_input.request_quit = true;
				}
				elif (vk_code == VK_LEFT) {
					update_game_button(&game_input.left_arrow, is_down);
				}
				elif (vk_code == VK_RIGHT) {
					update_game_button(&game_input.right_arrow, is_down);
				}
				elif (vk_code == VK_UP) {
					update_game_button(&game_input.up_arrow, is_down);
				} 
				elif (vk_code == VK_DOWN) {
					update_game_button(&game_input.down_arrow, is_down);
				}
				elif (key_down && vk_code == ord("L")) {
					if (key_down) {
						win_state.is_recording = !win_state.is_recording;
					}
				}
				elif (key_down && vk_code == ord("P")) {
					if (key_down) {
						win_state.is_replaying = !win_state.is_replaying;
					}
				}
			} else {
				TranslateMessage(&msg);
				DispatchMessageA(&msg);
			}
		}
	}

	var perf_count_freq: i64;
	let get_duration = fun (c0: i64; c1: i64) => f32
	{
		return (c0 - c1)@f32 / @f32 perf_count_freq;
	}

	let get_perf_counter = fun() => i64
	{
		var result: i64;
		QueryPerformanceCounter(&result);
		return result;
	}

	let record_input = fun
	(
		new_input: game_input*;
		game_memory: game_memory*;
		win_state: win_state*;
	) => void
	{
		if (win_state.recording_started && win_state.is_recording && win_state.is_replaying) {
			win_state.is_recording = false;
		}
		if (win_state.is_recording && !win_state.recording_started) {
			if (win_state.is_replaying) {
				return;
			}
			win_state.recording_started = true;
			win_state.recording_file_handle = 
				CreateFileA(cstr("input_recording.dat\0"), GENERIC_WRITE, 0, nullptr, CREATE_ALWAYS, 0, 0);
				var bytes_written = 0;

			var total_size = game_memory.permanent_size + game_memory.transient_size;
			assert(total_size < gigabytes(4));
			WriteFile(win_state.recording_file_handle, game_memory.permanent_data, total_size@i32, &bytes_written, nullptr);
		}
		if (!win_state.is_recording && win_state.recording_started) {
			CloseHandle(win_state.recording_file_handle);
			win_state.recording_started = false;
		}
		if (win_state.is_recording)
		{
			assert(!win_state.is_replaying);
			var bytes_written = 0;
			WriteFile(win_state.recording_file_handle, new_input@i8*, size_of(game_input)@i32, &bytes_written, nullptr);
		}
	}

	let replay_input = fun
	(
		new_input: game_input*;
		game_memory: game_memory*;
		win_state: win_state*;
	) => void
	{
		if (win_state.replaying_started && win_state.is_replaying && win_state.is_recording) {
			win_state.is_replaying = false;
		}
		if (win_state.is_replaying && !win_state.replaying_started) {
			if (win_state.is_recording) {
				return;
			}
			win_state.replaying_started = true;
			win_state.replaying_file_handle = 
				CreateFileA(cstr("input_recording.dat\0"), GENERIC_READ, FILE_SHARE_READ, nullptr, OPEN_EXISTING, 0, 0);
			var bytes_read = 0;
			var total_size = game_memory.permanent_size + game_memory.transient_size;
			assert(total_size < gigabytes(4));
			ReadFile(win_state.replaying_file_handle, game_memory.permanent_data, total_size@i32, &bytes_read, nullptr);
		}
		if (!win_state.is_replaying && win_state.replaying_started) {
			CloseHandle(win_state.replaying_file_handle);
			win_state.replaying_started = false;
			var cleared_input = game_input {};
			*new_input = cleared_input; 
			// TODO: this crashes backend.
			// probably struct constructer must be able to _not_ return a pointer e.g. insert a load!
			// *new_input = game_input {};
		}
		if (win_state.is_replaying)
		{
			assert(!win_state.is_recording);
			var bytes_read = 0;
			ReadFile(win_state.replaying_file_handle, new_input@i8*, size_of(game_input)@i32, &bytes_read, nullptr);
			if (bytes_read == 0)
			{
				CloseHandle(win_state.replaying_file_handle);
				win_state.replaying_started = false;
			}	
		}
	}

	let PROCESS_SYSTEM_DPI_AWARE = 1;
	let SetProcessDpiAwareness = extern fun(
    	value: i32;
	) => i32;


	let win_audio_state = struct(
		game: game_interface*;
		game_memory: game_memory*;
	);

	let wasapi_audio_callback = fun(request: Wasapi::audio_fill_request*) => void {
		var audio_state = request.user_data@win_audio_state*;
		var game_sound_output: game_sound_output;
		game_sound_output.samples_per_second = request.samples_per_second;
		game_sound_output.num_channels = request.num_channels;
		game_sound_output.start_buffer = request.start_buffer;
		game_sound_output.end_buffer = request.end_buffer;

		if (audio_state.game.is_loaded) {
			audio_state.game.output_sound(audio_state.game_memory, &game_sound_output);
		}
	}
	
	[
	"compile.entry",
	"compile.cpu"   : "native",
	"compile.output": "win_handmade.exe",
	"compile.opt"   : "0",
	"compile.debuginfo": "true",
	"compile.ll"    : "true",
	"compile.run"   : "true",
	"compile.path"  : "C:\Program Files (x86)\Windows Kits\10\Lib\10.0.14393.0\um\x64, lib",
	"compile.libs"  : "kernel32.lib, user32.lib, gdi32.lib, winmm.lib, shcore.lib, libopenlibm.a, ole32.lib"
	]
	let main = fun () => void 
	{
		QueryPerformanceFrequency(&perf_count_freq);
	#if WIN_10
		SetProcessDpiAwareness(PROCESS_SYSTEM_DPI_AWARE);
	#endif

		var error = 0;

		let desired_sheduler_granularity = 1;
		error = timeBeginPeriod(desired_sheduler_granularity);
		assert(error == 0);

		window.width = 1280;
		window.height = 720;

		create_backbuffer(&backbuffer, window.width, window.height);

		var class_name  = cstr("PragmaScriptWindowClass\0");
		var window_name = cstr("handmade-pragmascript\0");
		var instance = GetModuleHandleA(null);
		var window_class = WNDCLASSEX {};
		window_class.cbSize = size_of(WNDCLASSEX)@i32;
		window_class.style = (CS_HREDRAW|CS_VREDRAW);
		window_class.lpfnWndProc = @ptr main_window_callback;
		window_class.hInstance = instance;
		window_class.lpszClassName = class_name;
		window_class.hCursor = LoadCursorA(null, IDC_ARROW);
		RegisterClassExA(&window_class);

		window.handle = CreateWindowExA(
			WS_EX_TOPMOST,
			class_name,
			window_name,
			WS_OVERLAPPEDWINDOW|WS_VISIBLE,
			CW_USEDEFAULT,
			CW_USEDEFAULT,
			window.width,
			window.height,
			null,
			null,
			instance,
			null
		);
		window.dc = GetDC(window.handle);
		update_window_rect();

		var monitor_hz = 60;
		// var win_vrefresh = GetDeviceCaps(window.dc, VREFRESH);
		// debug_print_i32("VREFRESH", win_vrefresh);
		// if (win_vrefresh > 1)
		// {
		// 	monitor_hz = win_vrefresh;
		// 	debug_print_i32("monitor_hz", monitor_hz);
		// }
		
		var game_target_fps = monitor_hz;
		var target_sec_per_frame = 1.0 / @f32 game_target_fps;

		var base_address = nullptr;
		if (HANDMADE_INTERAL) {
			base_address = @ptr terabytes(2);
		} else {
			base_address = nullptr;
		}

		var platform = platform_interface {};
		platform.write_file = platform_write_file;
		platform.read_file = platform_read_file;
		platform.free_file_memory = platform_free_file_memory;

		var game_memory = game_memory {}; 
		game_memory.permanent_size = megabytes(1);
		game_memory.transient_size = 0;
		var total_size = game_memory.permanent_size + game_memory.transient_size;
		game_memory.permanent_data = VirtualAlloc(base_address, total_size, MEM_RESERVE|MEM_COMMIT, PAGE_READWRITE);
		assert(game_memory.permanent_data != nullptr);
		game_memory.transient_data = game_memory.permanent_data + game_memory.permanent_size;
		assert(game_memory.transient_data != nullptr);
		game_memory.platform = &platform;

		var win_state = win_state{};
		win_state.game_memory = &game_memory;
		
		var game = load_game_dll(); 
			
		var running = true;
		var frames = 0;
		var last_fps: f32 = 60;
		var last_counter_debug_stats: i64 = 0;

		var current_cycle_count: i64;
		var last_cycle_count = _rdtsc();

		var game_start_time = get_perf_counter();

		var game_input = game_input {};
		
		var win_audio_state: win_audio_state;
		win_audio_state.game = &game;
		win_audio_state.game_memory = &game_memory;
		var wasapi_audio_state = Wasapi::audio_state {};
		wasapi_audio_state.callback = wasapi_audio_callback@Wasapi::audio_fill_callback;
		wasapi_audio_state.user_data = &win_audio_state@ptr;		
		Wasapi::audio_init(&wasapi_audio_state);

		
		var game_sound_output = game_sound_output { };
		

		var game_time = game_time{};

		var frame_start_counter = get_perf_counter() - @i64(target_sec_per_frame * @f32 perf_count_freq);
		while (running) {
			var last_frame_start_counter = frame_start_counter;
			frame_start_counter = get_perf_counter();

			process_pending_messages(&game_input, &win_state);

			var game_render_target = game_render_target { };
			game_render_target.memory = backbuffer.memory;
			game_render_target.width = backbuffer.width;
			game_render_target.height = backbuffer.height;
			game_render_target.pitch = backbuffer.pitch;
			
			var t = get_duration(frame_start_counter, game_start_time);

			game_time.t = t;
			game_time.dt = get_duration(frame_start_counter, last_frame_start_counter);
			game_input.time = game_time;
			game_input.request_quit |= window_requests_quit;
			get_mouse_input(&game_input);
			
			record_input(&game_input, &game_memory, &win_state);
			replay_input(&game_input, &game_memory, &win_state);

			assert(game.is_loaded);
			running = game.update_and_render(&game_memory, &game_input, &game_render_target);

			blit_to_screen(&backbuffer);

			current_cycle_count = _rdtsc();
			var frame_end_counter = get_perf_counter();
			var wait_time = get_duration(frame_end_counter, frame_start_counter);
			while (wait_time < target_sec_per_frame) {
				var sleep_ms = @i32 (1000.0 * (target_sec_per_frame - wait_time));
				if (sleep_ms > 0) {
					Sleep(sleep_ms);
				}
				frame_end_counter = get_perf_counter();
				wait_time = get_duration(frame_end_counter, frame_start_counter);
			}
			frames++;

			if (frame_end_counter - last_counter_debug_stats > perf_count_freq) {
				if (DEBUG_DRAW_STATS) {
					last_counter_debug_stats = frame_end_counter;
					var cycles_elapsed = current_cycle_count - last_cycle_count;
					var frame_ms = 1000.0 * game_input.time.dt;
					print("\n");
					print(frame_ms);
					print("ms/f   ");
					print(1000.0 / frame_ms);
					last_fps = 0.5 * last_fps + (1000.0 / frame_ms);
					print("fps   ");
					print(cycles_elapsed@f64 / 1000000.0);
					print("mcycles   ");
					debug_print("dt", game_input.time.dt);
				}
				refresh_game_dll(&game);
			}
			
			last_cycle_count = current_cycle_count;
		}	

		ExitProcess(0);
	}
}