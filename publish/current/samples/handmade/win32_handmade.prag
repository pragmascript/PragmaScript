import "../preamble.prag"
import "handmade_interface.prag"

namespace Handmade.Platform
{
	with Windows;

	// https://github.com/ecxr/handmadehero/blob/master/code/win32_handmade.cpp

	// TODO: 
	// - Saved game locations
	// - Getting a handle to our own executable file
	// - Asset loading pppath
	// - Threading
	// - Raw input
	// - Sleep/timeBeginPeriod
	// - ClipCursor for multimonitor support
	// - Fullscreen support
	// - WM_SETCURSOR (cursor visibility)
	// - QueryCancelAutoplay
	// - WM_ACTIATEAPP (when we are not active app)
	// - Blit speed improvements (bitblit)
	// - Hardware accelereration
	// - GetKeyboardLayout (for internationalization)


	let HANDMADE_INTERAL = true;
	let DEBUG_DRAW_STATS = true;

	let offscreen_buffer = struct
	(
		info:   BITMAPINFO;
		memory: ptr;
		width:  i32;
		height: i32;
		pitch:  i32;
	);
	let window_info = struct 
	(
		handle: mm;
		dc: mm;
		width: i32;
		height: i32;
	);

	let win_state = struct
	(
		game_memory: game_memory*;
		is_recording: bool;
		is_replaying: bool;
		recording_started: bool;
		replaying_started: bool;
		recording_file_handle: mm;
		replaying_file_handle: mm;
	);

	var backbuffer: offscreen_buffer;
	var window: window_info;
	var window_requests_quit = false;

	let sound_output = struct
	(
		sound_buffer: IDirectSoundBuffer*;
		samples_per_second: i32;
		bytes_per_chunk: i32;
		bytes_per_frame: i32;
		buffer_size: i32;
		buffer_start: ptr;
		
		byte_last_written: i32;
		last_play_cursor: i32;
		last_play_cursor_change_time: i64;

		next_write_positon: i32;
		next_bytes_to_write: i32;
		first_write: bool;
	);
	
	// let platform_write_file = fun (name: string; buffer: i8[]) => bool;
	let platform_write_file: platform_write_file = 
	{
		var handle = CreateFileA(cstr(name), GENERIC_WRITE, 0, nullptr, CREATE_ALWAYS, 0, 0);
		if (handle == null) {
			CloseHandle(handle);
			return false;
		}
		var bytes_written: i32 = 0;
		var result = WriteFile(handle, buffer.data, buffer.length, &bytes_written, nullptr);
		if (result != 0) {
			CloseHandle(handle);
			assert(buffer.length == bytes_written);
			return true;
		} else {
			CloseHandle(handle);
			return false;
		}

		CloseHandle(handle);
		return false;
	}

	// let platform_read_file = fun (name: string) => i8[];
	let platform_read_file: platform_read_file =  
	{
		var result: i8[];
		result.data = nullptr;
		result.length = 0;
		var handle = CreateFileA(cstr(name), GENERIC_READ, FILE_SHARE_READ, nullptr, OPEN_EXISTING, 0, 0);
		if (handle == null) {
			return result;
		}

		var size: i64 = 0;
		var fsr = GetFileSizeEx(handle, &size);
		if (fsr == 0 || size == 0) {
			CloseHandle(handle);
			return result;
		}

		var buffer = VirtualAlloc(nullptr, (mm)size, MEM_COMMIT, PAGE_READWRITE);
		if ((mm)buffer == null) {
			CloseHandle(handle);
			return result;
		}
		assert((mm)size <=\ gigabytes(4));
		var size_32 = (i32)size;
		var bytes_read: i32;
		var rfr = ReadFile(handle, buffer, size_32, &bytes_read,  nullptr);
		assert(bytes_read == size_32);
		if (rfr == 0) {
			VirtualFree(buffer, null, MEM_RELEASE);
			CloseHandle(handle);
			return result;		
		}

		result.data = buffer;
		result.length = size_32;
		CloseHandle(handle);
		return result;
	} 

	let platform_free_file_memory: platform_free_file_memory =  
	{
		VirtualFree(mem, null, MEM_RELEASE);
	}

	let game_interface = struct
	(
		library: mm;
		update_and_render: game_update_and_render;
		output_sound: game_output_sound;
		is_loaded: bool;
		dll_last_write_time: i64;
	);

	let refresh_game_dll = fun
	(
		game: game_interface*;
	) => void
	{
		var current_write_time = get_last_write_time("handmade.dll\0");
		if (current_write_time != game.dll_last_write_time) {
			unload_game_dll(game);
			*game = load_game_dll();
		}
	}

	let load_game_dll = fun () => game_interface
	{
		var source_str = "handmade.dll\0";
		var temp_str = "handmade_temp.dll\0";
		var result = game_interface{};

		result.dll_last_write_time = get_last_write_time(source_str);
		var copy_result = CopyFileA(cstr(source_str), cstr(temp_str), 0);
		
		result.library = LoadLibraryA(cstr(temp_str));
		result.update_and_render = (game_update_and_render)GetProcAddress(result.library, cstr("game_update_and_render\0"));
		assert((ptr)result.update_and_render != nullptr);
		result.output_sound = (game_output_sound)GetProcAddress(result.library, cstr("game_output_sound\0"));
		assert((ptr)result.output_sound != nullptr);
		result.is_loaded = true;

		print_string("load library\n");
		return result;
		
	}

	let unload_game_dll = fun (@interface: game_interface*) => void
	{
		if (library != null) {
			var result = FreeLibrary(library);
			assert(result != 0);
		}
		update_and_render = (game_update_and_render)nullptr;
		output_sound = (game_output_sound)nullptr;
		is_loaded = false;
		library = null;
		print_string("unload library\n");
	}

	let get_last_write_time = fun
	(
		filename: string;
	) => i64
	{
		var cs = cstr(filename);
		var data: WIN32_FILE_ATTRIBUTE_DATA;
		GetFileAttributesExA(cstr("handmade.dll\0"), GetFileExInfoStandard, &data);
		return data.LastWriteTime;
	}

	let init_direct_sound = fun 
	(
		hwnd: mm; 
		buffer_size: i32; 
		samples_per_second: i32;
	) => IDirectSoundBuffer*
	{ 
		var dsound_library = LoadLibraryA(cstr("dsound.dll\0"));
		assert(dsound_library != null);

		var DirectSoundCreate: fun 
		(
			GuidDevice: ptr;
			ds: IDirectSound**;
			unused: ptr;
		) => i32;
		*((ptr*)(&DirectSoundCreate)) = (ptr)GetProcAddress(dsound_library, cstr("DirectSoundCreate\0"));
		assert((ptr)DirectSoundCreate != nullptr);

		var dsound_obj: IDirectSound*;
		var error = DirectSoundCreate(nullptr, &dsound_obj, nullptr); 
		assert(error == DS_OK);
		error = dsound_obj.vtable.SetCooperativeLevel(dsound_obj, hwnd, DSSCL_PRIORITY);
		assert(error == DS_OK);

		var desc_primary = DSBUFFERDESC { };
		desc_primary.flags = DSBCAPS_PRIMARYBUFFER;
		desc_primary.size = (i32)(size_of(DSBUFFERDESC));
		desc_primary.bufferBytes = 0;
		desc_primary.reserved = 0;
		desc_primary.fxFormat = (WAVEFORMATEX*)nullptr;

		var primary_buffer: IDirectSoundBuffer*;
		error = dsound_obj.vtable.CreateSoundBuffer(dsound_obj, &desc_primary, &primary_buffer, nullptr);
		assert(error == DS_OK);

		var wave_format = WAVEFORMATEX {};
		wave_format.formatTag = WAVE_FORMAT_PCM;
		wave_format.nChannels = 2;
		wave_format.nBitsPerSample = 16;
		wave_format.nSamplesPerSec = samples_per_second;
		wave_format.nBlockAlign = (wave_format.nChannels*wave_format.nBitsPerSample) / (i16)8;
		wave_format.nAvgBytesPerSec = wave_format.nSamplesPerSec * (i32)wave_format.nBlockAlign;
		wave_format.size = 0;
		error = primary_buffer.vtable.SetFormat(primary_buffer, &wave_format);
		assert(error == DS_OK);
		
		var desc_secondary = DSBUFFERDESC { };
		desc_secondary.size = (i32)(size_of(DSBUFFERDESC));
		desc_secondary.flags = DSBCAPS_GETCURRENTPOSITION2;
		desc_secondary.bufferBytes = buffer_size;
		desc_secondary.fxFormat = &wave_format;
		desc_secondary.reserved = 0;

		var secondary_buffer: IDirectSoundBuffer*;
		error = dsound_obj.vtable.CreateSoundBuffer(dsound_obj, &desc_secondary, &secondary_buffer, nullptr);
		assert(error == DS_OK);
		return secondary_buffer;
	}

	let clear_sound_buffer = fun
	(
		@sound_output: sound_output*;
	) => void
	{
		var region1: ptr;
		var region1_size: i32;
		var region2: ptr;
		var region2_size: i32;
		
		var error = sound_buffer.vtable.Lock(sound_buffer, 
			0, buffer_size, 
			&region1, &region1_size,
			&region2, &region2_size,
			0);
		assert(error == DS_OK);
		var dest_sample = (i32*)region1;
		var sample_count = region1_size / bytes_per_chunk;
		for (var i: i32 = 0; i < sample_count; ++i) {
			*dest_sample++ = 0;
		}
		buffer_start = region1;
		assert(region2_size == 0);

		error = sound_buffer.vtable.Unlock(sound_buffer,
			region1, region1_size, 
			region2, region2_size);
		assert(error == DS_OK);
	}

	let fill_sound_buffer = fun 
	(
		@sound_output: sound_output*;
		game_sound_output: game_sound_output*;
	) => void 
	{
		var byte_lock_position = next_write_positon;
		var bytes_to_write = next_bytes_to_write;
		var region1: ptr;
		var region1_size: i32;
		var region2: ptr;
		var region2_size: i32;

		var error = sound_buffer.vtable.Lock(sound_buffer, 
			byte_lock_position, bytes_to_write, 
			&region1, &region1_size,
			&region2, &region2_size,
			0);
		var sample_count_1 = region1_size / sound_output.bytes_per_chunk;
		var sample_count_2 = region2_size / sound_output.bytes_per_chunk;
		
		if (error == DS_OK)
		{
			assert(sample_count_1 + sample_count_2 == game_sound_output.sample_count);
			var source_sample = game_sound_output.sample_out;
			var dest_sample = (i16*)region1;
			for (var i: i32 = 0; i < sample_count_1; ++i) {
				*dest_sample++ = *source_sample++;
				*dest_sample++ = *source_sample++;
			}
			dest_sample = (i16*)region2;
			for (var i: i32 = 0; i < sample_count_2; ++i) {
				*dest_sample++ = *source_sample++;
				*dest_sample++ = *source_sample++;
			}
			error = sound_output.sound_buffer.vtable.Unlock(sound_buffer,
				region1, region1_size, 
				region2, region2_size);
			assert(error == DS_OK);
			
			if (sample_count_2 > 0) {
				var last_sample = (i8*)((i32*)region2 + sample_count_2);
				byte_last_written = (i32)(last_sample - sound_output.buffer_start);
			}
			elif (sample_count_1 > 0) {
				var last_sample = (i8*)((i32*)region1 + sample_count_1);
				byte_last_written = (i32)(last_sample - sound_output.buffer_start);
			}

			assert(sample_count_2 == 0 || (sample_count_1 != 0 && sample_count_2 != 0));
			assert(byte_last_written >= 0 && byte_last_written < buffer_size);
		}
	}

	let update_next_sound_write_positon = fun 
	(
		@sound_output: sound_output*;
	) => void  
	{
		var play_cursor: i32;
		var write_cursor: i32;
		var error = sound_buffer.vtable.GetCurrentPosition(sound_buffer, &play_cursor, &write_cursor);
		assert(error == DS_OK);

		if (play_cursor == write_cursor) {
			return;
		}
		// we haven't written anything yet
		if (!first_write) {
			byte_last_written = write_cursor;
			last_play_cursor_change_time = get_perf_counter();
		}

		if (play_cursor == last_play_cursor) {
			var time: i64;
			QueryPerformanceCounter(&time);
			var cursor_dt = (f64)(time - last_play_cursor_change_time) / (f64)perf_count_freq;
			var d_bytes = sound_output.bytes_per_chunk * (i32)(cursor_dt * (f64)sound_output.samples_per_second);
			play_cursor += d_bytes;
			write_cursor += d_bytes;
			play_cursor = play_cursor % sound_output.buffer_size;
			write_cursor = write_cursor % sound_output.buffer_size;
		} else {
			last_play_cursor = play_cursor;
			last_play_cursor_change_time = get_perf_counter();
		}

		var position_to_write = byte_last_written;
		if (play_cursor > last_play_cursor) {
			if (byte_last_written >= last_play_cursor && byte_last_written <= play_cursor) {
				position_to_write = write_cursor;
			}		
		}
		if (play_cursor < last_play_cursor) {
			if (byte_last_written >= last_play_cursor || byte_last_written <= play_cursor) {
				position_to_write = write_cursor;
			}		
		}
		var delta_cursor: i32;
		if (play_cursor < write_cursor) {
			if (position_to_write >= play_cursor && position_to_write < write_cursor) {
				position_to_write = write_cursor;
			}
			delta_cursor = write_cursor - play_cursor;
		}
		if (play_cursor > write_cursor) {
			if (position_to_write >= play_cursor || position_to_write < write_cursor) {
				position_to_write = write_cursor;
			}
			delta_cursor = (buffer_size - play_cursor) + write_cursor;
		}
		assert(delta_cursor > 0);

		var target_cursor: i32;
		var safety_bytes = bytes_per_frame / 2;
		target_cursor = write_cursor;
		
		while (target_cursor <= write_cursor + delta_cursor) {
			target_cursor += bytes_per_frame;
		}
		target_cursor += safety_bytes;
		if (target_cursor > buffer_size) {
			target_cursor -= buffer_size;
		}

		var bytes_to_write = 0;
		if (target_cursor > play_cursor) {
			if (position_to_write < target_cursor && position_to_write > play_cursor) {
				bytes_to_write = target_cursor - position_to_write;
			}
		}
		if (target_cursor < play_cursor) {
			if (position_to_write > play_cursor) {
				bytes_to_write = buffer_size - position_to_write;
				bytes_to_write += target_cursor;
			}
			if (position_to_write < target_cursor) {
				bytes_to_write = target_cursor - position_to_write;
			}
		}

		sound_output.next_write_positon = position_to_write;
		sound_output.next_bytes_to_write = bytes_to_write;

		if (!sound_output.first_write && bytes_to_write > 0) {
			sound_output.first_write = true;
		}
		// if (oops) {
		// 	print_string("oops\n");
		// }
		// debug_print_i32("pc ", play_cursor);
		// debug_print_i32("last pos", sound_output.byte_last_written);
		// debug_print_i32("ptw", position_to_write);
		// debug_print_i32("wc ", write_cursor);
		// debug_print_i32("tc ", target_cursor);
		// debug_print_i32("btw", bytes_to_write);
		// debug_print_i32("bytes per frame", sound_output.bytes_per_frame);
		// print_string("---------------------------\n");
	}

	let create_backbuffer = fun (@buffer: offscreen_buffer*; target_width: i32; target_height: i32) => void 
	{
		let bytes_per_pixel = 4;
		width = target_width;
		height = target_height;
		info.header.biSize = 40;
		info.header.biWidth = width;
		info.header.biHeight = -height;
		info.header.biPlanes = 1;
		info.header.biBitCount = 32;
		info.header.biCompression = BI_RGB;
		var bitmap_size = bytes_per_pixel * width * height;
		if (memory != nullptr) {
			VirtualFree(buffer.memory, null, MEM_RELEASE);
		}
		memory = VirtualAlloc(nullptr, (mm\)bitmap_size, MEM_COMMIT, PAGE_READWRITE);
		pitch = buffer.width * bytes_per_pixel;
	}

	let blit_to_screen = fun (buffer: offscreen_buffer*) => void 
	{
		StretchDIBits(window.dc,
			0, 0, buffer.width, buffer.height,
			0, 0, buffer.width, buffer.height,
			buffer.memory, &buffer.info,
			DIB_RGB_COLORS, SRCCOPY
			);
	}

	let update_window_rect = fun () => void 
	{
		var rect: RECT;
		GetClientRect(window.handle, &rect); 
		window.width = rect.right - rect.left;
		window.height = rect.bottom - rect.top;
	}

	let main_window_callback = fun 
	(
		window_handle: mm;
		message: i32;
		w_param: mm;
		l_param: mm;
	) => mm 
	{
		var result = null;
		if (message == WM_SIZE) {
			update_window_rect();	
		}
		elif (message == WM_CLOSE) {
			window_requests_quit = true;
		}
		elif (message == WM_DESTROY) {
			window_requests_quit = true;
		}
		elif (message == WM_PAINT) {
			var paint: PAINTSTRUCT;
			var context = BeginPaint(window_handle, &paint);
			blit_to_screen(&backbuffer);
			EndPaint(window_handle, &paint);
		} 
		else {
			result = DefWindowProcA(window_handle, message, w_param, l_param);
		} 
		return result;
	}


	let update_game_button = fun (button: game_button*; is_pressed: bool) => void 
	{
		if (is_pressed && !button.pressed) {
			button.down = true;
		}
		if (!is_pressed && button.pressed) {
			button.up = true;
		}
		button.pressed = is_pressed;
	}

	let get_mouse_input = fun (game_input: game_input*) => void 
	{
		var cursor_pos: POINT;
		GetCursorPos(&cursor_pos);
		ScreenToClient(window.handle, &cursor_pos);
		game_input.mouse_x = cursor_pos.x;
		game_input.mouse_y = cursor_pos.y;
		update_game_button(&game_input.left_mb, (GetKeyState(VK_LBUTTON) & (i16)(1 << 15)) != 0);
		update_game_button(&game_input.right_mb, (GetKeyState(VK_RBUTTON) & (i16)(1 << 15)) != 0);
		update_game_button(&game_input.middle_mb, (GetKeyState(VK_MBUTTON) & (i16)(1 << 15)) != 0);
	}

	let process_pending_messages = fun 
	(
		game_input: game_input*;
		win_state: win_state*;
	) => void 
	{
		var msg: MSG;
		while (PeekMessageA(&msg, null, 0, 0, PM_REMOVE) != 0) {
			var message = msg.message;
			if (message == WM_QUIT) {
				game_input.request_quit = true;
			} 
			elif (message == WM_SYSKEYDOWN
				|| message == WM_SYSKEYUP
				|| message == WM_KEYDOWN
				|| message == WM_KEYUP) {

				var w_param = (i32)(msg.wParam >> (mm)32);
				var l_param = (i32)(msg.lParam >> (mm)32);
				var vk_code = w_param;

				var was_down = (l_param & (1 << 30)) != 0;
				var is_down = (l_param & (1 << 31)) == 0;
				var key_up = was_down && !is_down;
				var key_down = !was_down && is_down; 

				if(vk_code == VK_ESCAPE) {
					game_input.request_quit = true;
				}
				elif (vk_code == VK_LEFT) {
					update_game_button(&game_input.left_arrow, is_down);
				}
				elif (vk_code == VK_RIGHT) {
					update_game_button(&game_input.right_arrow, is_down);
				}
				elif (vk_code == VK_UP) {
					update_game_button(&game_input.up_arrow, is_down);
				} 
				elif (vk_code == VK_DOWN) {
					update_game_button(&game_input.down_arrow, is_down);
				}
				elif (key_down && vk_code == ord("L")) {
					if (key_down) {
						win_state.is_recording = !win_state.is_recording;
					}
				}
				elif (key_down && vk_code == ord("P")) {
					if (key_down) {
						win_state.is_replaying = !win_state.is_replaying;
					}
				}
			} else {
				TranslateMessage(&msg);
				DispatchMessageA(&msg);
			}
		}
	}

	var perf_count_freq: i64;
	let get_duration = fun (c0: i64; c1: i64) => f32
	{
		return (f32)(c0 - c1) / (f32)perf_count_freq;
	}

	let get_perf_counter = fun() => i64
	{
		var result: i64;
		QueryPerformanceCounter(&result);
		return result;
	}

	let record_input = fun
	(
		new_input: game_input*;
		game_memory: game_memory*;
		win_state: win_state*;
	) => void
	{
		if (win_state.recording_started && win_state.is_recording && win_state.is_replaying) {
			win_state.is_recording = false;
		}
		if (win_state.is_recording && !win_state.recording_started) {
			if (win_state.is_replaying) {
				return;
			}
			win_state.recording_started = true;
			win_state.recording_file_handle = 
				CreateFileA(cstr("input_recording.dat\0"), GENERIC_WRITE, 0, nullptr, CREATE_ALWAYS, 0, 0);
				var bytes_written = 0;

			var total_size = game_memory.permanent_size + game_memory.transient_size;
			assert(total_size < gigabytes(4));
			WriteFile(win_state.recording_file_handle, game_memory.permanent_data, (i32)total_size, &bytes_written, nullptr);
		}
		if (!win_state.is_recording && win_state.recording_started) {
			CloseHandle(win_state.recording_file_handle);
			win_state.recording_started = false;
		}
		if (win_state.is_recording)
		{
			assert(!win_state.is_replaying);
			var bytes_written = 0;
			WriteFile(win_state.recording_file_handle, (i8*)new_input, (i32)(size_of(game_input)), &bytes_written, nullptr);
		}
	}

	let replay_input = fun
	(
		new_input: game_input*;
		game_memory: game_memory*;
		win_state: win_state*;
	) => void
	{
		if (win_state.replaying_started && win_state.is_replaying && win_state.is_recording) {
			win_state.is_replaying = false;
		}
		if (win_state.is_replaying && !win_state.replaying_started) {
			if (win_state.is_recording) {
				return;
			}
			win_state.replaying_started = true;
			win_state.replaying_file_handle = 
				CreateFileA(cstr("input_recording.dat\0"), GENERIC_READ, FILE_SHARE_READ, nullptr, OPEN_EXISTING, 0, 0);
			var bytes_read = 0;
			var total_size = game_memory.permanent_size + game_memory.transient_size;
			assert(total_size < gigabytes(4));
			ReadFile(win_state.replaying_file_handle, game_memory.permanent_data, (i32)total_size, &bytes_read, nullptr);
		}
		if (!win_state.is_replaying && win_state.replaying_started) {
			CloseHandle(win_state.replaying_file_handle);
			win_state.replaying_started = false;
			var cleared_input = game_input {};
			*new_input = cleared_input; 
			// TODO: this crashes backend.
			// probably struct constructer must be able to _not_ return a pointer e.g. insert a load!
			// *new_input = game_input {};
		}
		if (win_state.is_replaying)
		{
			assert(!win_state.is_recording);
			var bytes_read = 0;
			ReadFile(win_state.replaying_file_handle, (i8*)new_input, (i32)(size_of(game_input)), &bytes_read, nullptr);
			if (bytes_read == 0)
			{
				CloseHandle(win_state.replaying_file_handle);
				win_state.replaying_started = false;
			}	
		}
	}

	
	[
	"compile.entry",
	"compile.output": "win_handmade.exe",
	"compile.opt"   : "0",
	"compile.ll"    : "true",
	"compile.run"   : "false",
	"compile.path"  : "C:\Program Files (x86)\Windows Kits\10\Lib\10.0.14393.0\um\x64, lib",
	"compile.libs"  : "kernel32.lib, user32.lib, gdi32.lib, winmm.lib, libopenlibm.a"
	]
	let main = fun () => void 
	{
		QueryPerformanceFrequency(&perf_count_freq);
		var error = 0;

		let desired_sheduler_granularity = 1;
		error = timeBeginPeriod(desired_sheduler_granularity);
		assert(error == 0);

		window.width = 1024;
		window.height = 768;

		create_backbuffer(&backbuffer, window.width, window.height);

		var class_name  = cstr("PragmaScriptWindowClass\0");
		var window_name = cstr("handmade-pragmascript\0");
		var instance = GetModuleHandleA(null);
		var window_class = WNDCLASSEX {};
		window_class.cbSize = (i32)(size_of(WNDCLASSEX));
		window_class.style = (CS_HREDRAW|CS_VREDRAW);
		window_class.lpfnWndProc = (ptr)main_window_callback;
		window_class.hInstance = instance;
		window_class.lpszClassName = class_name;
		window_class.hCursor = LoadCursorA(null, IDC_ARROW);
		RegisterClassExA(&window_class);

		window.handle = CreateWindowExA(
			WS_EX_TOPMOST,
			class_name,
			window_name,
			WS_OVERLAPPEDWINDOW|WS_VISIBLE,
			CW_USEDEFAULT,
			CW_USEDEFAULT,
			window.width,
			window.height,
			null,
			null,
			instance,
			null
		);
		window.dc = GetDC(window.handle);
		update_window_rect();

		var monitor_hz = 60;
		// var win_vrefresh = GetDeviceCaps(window.dc, VREFRESH);
		// debug_print_i32("VREFRESH", win_vrefresh);
		// if (win_vrefresh > 1)
		// {
		// 	monitor_hz = win_vrefresh;
		// 	debug_print_i32("monitor_hz", monitor_hz);
		// }
		
		var game_target_fps = monitor_hz;
		var target_sec_per_frame = 1.0 / (f32)game_target_fps;
		
		var sound_output: sound_output;
		sound_output.samples_per_second = 48000;
		sound_output.bytes_per_chunk = 2 * (i32)(size_of(i16)); // 2 channels
		sound_output.bytes_per_frame = sound_output.bytes_per_chunk *sound_output.samples_per_second / game_target_fps;
		sound_output.buffer_size = sound_output.samples_per_second * sound_output.bytes_per_chunk * 1; // 1 seccond buffer
		
		var sound_buffer = init_direct_sound(window.handle, sound_output.buffer_size, sound_output.samples_per_second);
		sound_output.sound_buffer = sound_buffer;

		var game_sample_buffer = (i16*)VirtualAlloc(nullptr, (mm)sound_output.buffer_size, MEM_COMMIT, PAGE_READWRITE);
		assert((ptr)game_sample_buffer != nullptr);
		var base_address = nullptr;
		if (HANDMADE_INTERAL) {
			base_address = (ptr)terabytes(2);
		} else {
			base_address = nullptr;
		}

		var platform = platform_interface {};
		platform.write_file = platform_write_file;
		platform.read_file = platform_read_file;
		platform.free_file_memory = platform_free_file_memory;

		var game_memory = game_memory {}; 
		game_memory.permanent_size = megabytes(1);
		game_memory.transient_size = 0;
		var total_size = game_memory.permanent_size + game_memory.transient_size;
		game_memory.permanent_data = VirtualAlloc(base_address, total_size, MEM_RESERVE|MEM_COMMIT, PAGE_READWRITE);
		assert(game_memory.permanent_data != nullptr);
		game_memory.transient_data = game_memory.permanent_data + game_memory.permanent_size;
		assert(game_memory.transient_data != nullptr);
		game_memory.platform = &platform;

		var win_state = win_state{};
		win_state.game_memory = &game_memory;
		
		var game = load_game_dll(); 
		clear_sound_buffer(&sound_output);
		error = sound_buffer.vtable.Play(sound_buffer, 0, 0, DSBPLAY_LOOPING);
		assert(error == DS_OK);
			
		var running = true;
		var frames = 0;
		var last_fps: f32 = 60;
		var last_counter_debug_stats: i64 = 0;

		var current_cycle_count: i64;
		var last_cycle_count = _rdtsc();

		var game_start_time = get_perf_counter();

		var game_input = game_input {};
		var game_sound_output = game_sound_output { };
		game_sound_output.sample_out = game_sample_buffer;
		game_sound_output.samples_per_second = sound_output.samples_per_second;

		var game_time = game_time{};

		var frame_start_counter = get_perf_counter() - (i64)(target_sec_per_frame * (f32)perf_count_freq);
		while (running) {
			var last_frame_start_counter = frame_start_counter;
			frame_start_counter = get_perf_counter();

			process_pending_messages(&game_input, &win_state);

			var game_render_target = game_render_target { };
			game_render_target.memory = backbuffer.memory;
			game_render_target.width = backbuffer.width;
			game_render_target.height = backbuffer.height;
			game_render_target.pitch = backbuffer.pitch;
			
			var t = get_duration(frame_start_counter, game_start_time);
			// if ((i32)t > (i32)game_time.t) {
            // 	debug_print_i32("s", (i32)t);
			// }

			game_time.t = t;
			game_time.dt = get_duration(frame_start_counter, last_frame_start_counter);
			game_input.time = game_time;
			game_input.request_quit |= window_requests_quit;
			get_mouse_input(&game_input);
			
			record_input(&game_input, &game_memory, &win_state);
			replay_input(&game_input, &game_memory, &win_state);

			assert(game.is_loaded);
			running = game.update_and_render(&game_memory, &game_input, &game_render_target);

			update_next_sound_write_positon(&sound_output);
			game_sound_output.sample_count = sound_output.next_bytes_to_write / sound_output.bytes_per_chunk;

			assert(game.is_loaded);
			game.output_sound(&game_memory, &game_sound_output);

			fill_sound_buffer(&sound_output, &game_sound_output);
			if (!running) {
				// TODO(oragma): this crashes on exit in optimized builds!
				// clear_sound_buffer(&sound_output);
				// sound_output.sound_buffer.vtable.Stop();
			}

			blit_to_screen(&backbuffer);

			current_cycle_count = _rdtsc();
			var frame_end_counter = get_perf_counter();
			var wait_time = get_duration(frame_end_counter, frame_start_counter);
			while (wait_time < target_sec_per_frame) {
				var sleep_ms = (i32)(1000.0 * (target_sec_per_frame - wait_time));
				if (sleep_ms > 0) {
					Sleep(sleep_ms);
				}
				frame_end_counter = get_perf_counter();
				wait_time = get_duration(frame_end_counter, frame_start_counter);
			}
			frames++;

			if (frame_end_counter - last_counter_debug_stats > perf_count_freq) {
				if (DEBUG_DRAW_STATS) {
					last_counter_debug_stats = frame_end_counter;
					var cycles_elapsed = current_cycle_count - last_cycle_count;
					var frame_ms = 1000.0 * game_input.time.dt;
					print_string("\n");
					print_f32(frame_ms);
					print_string("ms/f   ");
					print_f32(1000.0 / frame_ms);
					last_fps = 0.5 * last_fps + (1000.0 / frame_ms);
					print_string("fps   ");
					print_f64((f64)cycles_elapsed / 1000000.0);
					print_string("mcycles   ");
					debug_print_f32("dt", game_input.time.dt);
				}
				refresh_game_dll(&game);
			}
			
			last_cycle_count = current_cycle_count;
		}	

		ExitProcess(0);
	}
}