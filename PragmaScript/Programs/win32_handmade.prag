import "handmade.prag"

// https://github.com/ecxr/handmadehero/blob/master/code/win32_handmade.cpp

// TODO: 
// - Saved game locations
// - Getting a handle to our own executable file
// - Asset loading pppath
// - Threading
// - Raw input
// - Sleep/timeBeginPeriod
// - ClipCursor for multimonitor support
// - Fullscreen support
// - WM_SETCURSOR (cursor visibility)
// - QueryCancelAutoplay
// - WM_ACTIATEAPP (when we are not active app)
// - Blit speed improvements (bitblit)
// - Hardware accelereration
// - GetKeyboardLayout (for internationalization)

let HANDMADE_INTERAL = true;
let DEBUG_DRAW_FPS = false;

let offscreen_buffer = struct
(
	info:   BITMAPINFO;
	memory: ptr;
	width:  i32;
	height: i32;
	pitch:  i32;
);
let window_info = struct 
(
	handle: mm;
	dc: mm;
	width: i32;
	height: i32;
);

var running = false;
var backbuffer: offscreen_buffer;
var window: window_info;
var game_input: game_input;

let sound_output = struct
(
	sound_buffer: IDirectSoundBuffer*;
	samples_per_second: i32;
	bytes_per_chunk: i32;
	buffer_size: i32;
	latency_byte_count: i32;
	sample_index: i32;
);


let kilobytes = fun (bytes: mm) => mm 
{
	return bytes * 1024 ;
}
let megabytes = fun (bytes: mm) => mm 
{
	return bytes * 1024 * 1024;
}
let gigabytes = fun (bytes: mm) => mm
{
	return bytes * 1024 * 1024 * 1024;
}
let terabytes = fun (bytes: mm) => mm
{
	return bytes * 1024 * 1024 * 1024 * 1024 ;
}

let init_direct_sound = fun (hwnd: mm; buffer_size: i32; samples_per_second: i32) => IDirectSoundBuffer*
{ 
	var dsound_library = LoadLibraryA(cstr("dsound.dll\0"));
	assert((mm)dsound_library != null);

	var DirectSoundCreate: fun 
	(
		GuidDevice: ptr;
		ds: IDirectSound**;
		unused: ptr;
	) => i32;
	*((ptr*)(&DirectSoundCreate)) = (ptr)GetProcAddress(dsound_library, cstr("DirectSoundCreate\0"));
	assert((mm)DirectSoundCreate != null);

	var dsound_obj: IDirectSound*;
	assert(DirectSoundCreate(nullptr, &dsound_obj, nullptr) == DS_OK);
	assert(dsound_obj.vtable.SetCooperativeLevel(dsound_obj, hwnd, DSSCL_PRIORITY) == DS_OK);

	var desc_primary = DSBUFFERDESC { };
	desc_primary.flags = DSBCAPS_PRIMARYBUFFER;
	desc_primary.size = (i32)(sizeof(DSBUFFERDESC));
	desc_primary.bufferBytes = 0;
	desc_primary.reserved = 0;
	desc_primary.fxFormat = (WAVEFORMATEX*)nullptr;

	var primary_buffer: IDirectSoundBuffer*;
	assert(dsound_obj.vtable.CreateSoundBuffer(dsound_obj, &desc_primary, &primary_buffer, nullptr) == DS_OK);

	var wave_format = WAVEFORMATEX {};
	wave_format.formatTag = WAVE_FORMAT_PCM;
	wave_format.nChannels = 2;
	wave_format.nBitsPerSample = 16;
	wave_format.nSamplesPerSec = samples_per_second;
	wave_format.nBlockAlign = (wave_format.nChannels*wave_format.nBitsPerSample) / (i16)8;
	wave_format.nAvgBytesPerSec = wave_format.nSamplesPerSec * (i32)wave_format.nBlockAlign;
	wave_format.size = 0;
	assert(primary_buffer.vtable.SetFormat(primary_buffer, &wave_format) == DS_OK);

	var desc_secondary = DSBUFFERDESC { };
	desc_secondary.size = (i32)(sizeof(DSBUFFERDESC));
	desc_secondary.flags = 0;
	desc_secondary.bufferBytes = buffer_size;
	desc_secondary.fxFormat = &wave_format;
	desc_secondary.reserved = 0;

	var secondary_buffer: IDirectSoundBuffer*;
	assert(dsound_obj.vtable.CreateSoundBuffer(dsound_obj, &desc_secondary, &secondary_buffer, nullptr) == DS_OK);
	return secondary_buffer;
}

let clear_sound_buffer = fun
(
	sound_output: sound_output*;
) => void
{
	var region1: ptr;
	var region1_size: i32;
	var region2: ptr;
	var region2_size: i32;
	var sound_buffer = sound_output.sound_buffer;
	var error = sound_buffer.vtable.Lock(sound_buffer, 
		0, sound_output.buffer_size, 
		&region1, &region1_size,
		&region2, &region2_size,
		0);
	assert(error == DS_OK);
	var dest_sample = (i32*)region1;
	var sample_count = region1_size / sound_output.bytes_per_chunk;
	for (var i: i32 = 0; i < sample_count; ++i) {
		*dest_sample++ = 0;
	}
	dest_sample = (i32*)region2;
	sample_count = region2_size / sound_output.bytes_per_chunk;
	for (var i: i32 = 0; i < sample_count; ++i) {
		*dest_sample++ = 0;
	}
	error = sound_output.sound_buffer.vtable.Unlock(sound_buffer,
		region1, region1_size, 
		region2, region2_size);
	assert(error == DS_OK);
}

let fill_sound_buffer = fun 
(
	sound_output: sound_output*;
	byte_lock_position: i32; bytes_to_write: i32;
	game_sound_output: game_sound_output*;
) => void 
{
	var region1: ptr;
	var region1_size: i32;
	var region2: ptr;
	var region2_size: i32;
	var sound_buffer = sound_output.sound_buffer;
	var error = sound_buffer.vtable.Lock(sound_buffer, 
		byte_lock_position, bytes_to_write, 
		&region1, &region1_size,
		&region2, &region2_size,
		0);
	var sample_count_0 = region1_size / sound_output.bytes_per_chunk;
	var sample_count_1 = region2_size / sound_output.bytes_per_chunk;
	// debug_print_i32("sample_count_0", sample_count_0, true);
	// debug_print_i32("sample_count_1", sample_count_1, true);
	// debug_print_i32("0+1", sample_count_0 + sample_count_1, true);
	// debug_print_i32("source_count", 48000/30, true);
	if (error == DS_OK)
	{
		var source_sample = game_sound_output.sample_out;
		var dest_sample = (i16*)region1;
		for (var i: i32 = 0; i < sample_count_0; ++i) {
			*dest_sample++ = *source_sample++;
			*dest_sample++ = *source_sample++;
		}
		dest_sample = (i16*)region2;
		for (var i: i32 = 0; i < sample_count_1; ++i) {
			*dest_sample++ = *source_sample++;
			*dest_sample++ = *source_sample++;
		}
		error = sound_output.sound_buffer.vtable.Unlock(sound_buffer,
			region1, region1_size, 
			region2, region2_size);
		assert(error == DS_OK);
		sound_output.sample_index += sample_count_0 + sample_count_1;
	}
}

let load_x_input = fun () => void 
{
	var x_input_lib = LoadLibraryA(cstr("xinput1_4.dll\0"));
	if (x_input_lib == null) {
		x_input_lib = LoadLibraryA(cstr("xinput1_3.dll\0"));
	}
	if (x_input_lib != null) {
		*((ptr*)(&XInputGetState)) = (ptr)GetProcAddress(x_input_lib, cstr("XInputGetState\0"));
		assert((mm)XInputGetState != null);
	}
}

let handle_x_input = fun () => void 
{
	for(var c_idx = 0; c_idx < XUSER_MAX_COUNT; ++c_idx) {
		var x_state : XINPUT_STATE;
		var x_result = XInputGetState(c_idx, &x_state); 
		if (x_result == XINPUT_SUCCESS)
		{
			// TODO: decode input
		}
		elif (x_result == XINPUT_DEVICE_NOT_CONNECTED)
		{
		}
	}
}

let create_backbuffer = fun (buffer: offscreen_buffer*; width: i32; height: i32) => void 
{
	let bytes_per_pixel = 4;
	buffer.width = width;
	buffer.height = height;
	buffer.info.header.biSize = 40;
	buffer.info.header.biWidth = width;
	buffer.info.header.biHeight = -height;
	buffer.info.header.biPlanes = 1;
	buffer.info.header.biBitCount = 32;
	buffer.info.header.biCompression = BI_RGB;
	var bitmap_size = bytes_per_pixel * width * height;
	if ((mm)buffer.memory != (mm)nullptr) {
		VirtualFree(buffer.memory, null, MEM_RELEASE);
	}
	buffer.memory = VirtualAlloc(nullptr, (mm)bitmap_size, MEM_COMMIT, PAGE_READWRITE);
	buffer.pitch = buffer.width * bytes_per_pixel;
}

let blit_to_screen = fun (buffer: offscreen_buffer*) => void 
{
    StretchDIBits(window.dc,
		0, 0, window.width, window.height,
		0, 0, buffer.width, buffer.height,
		buffer.memory, &buffer.info,
		DIB_RGB_COLORS, SRCCOPY
		);
}

let update_window_rect = fun () => void 
{
	var rect: RECT;
	GetClientRect(window.handle, &rect); 
	window.width = rect.right - rect.left;
	window.height = rect.bottom - rect.top;
}

let main_window_callback =
fun (window_handle: mm; message: i32; w_param: mm; l_param: mm) => mm 
{
	var result = null;
	if (message == WM_SIZE) {
		update_window_rect();	
	}
	elif (message == WM_CLOSE) {
		running = false;
	}
	elif (message == WM_DESTROY) {
		running = false;
	}
	elif (message == WM_PAINT) {
		var paint: PAINTSTRUCT;
		var context = BeginPaint(window_handle, &paint);
		blit_to_screen(&backbuffer);
		EndPaint(window_handle, &paint);
	} 
	elif (message == WM_SYSKEYDOWN 
		  || message == WM_SYSKEYUP
		  || message == WM_KEYDOWN
		  || message == WM_KEYUP) {

		var vk_code = (i32)w_param;
		var was_down = (l_param & (mm)(1 << 30)) != 0;
		var is_down = (l_param & (mm)(1 << 31)) == 0;
		var key_up = was_down && !is_down;
		var key_down = !was_down && is_down; 

		if(vk_code == VK_ESCAPE) {
			running = false;
		} elif (vk_code == VK_LEFT) {
			game_input.leftPressed = is_down;
		} elif (vk_code == VK_RIGHT) {
			game_input.rightPressed = is_down;
		} elif (vk_code == VK_UP) {
			game_input.upPressed = is_down;
		} elif (vk_code == VK_DOWN) {
			game_input.downPressed = is_down;
		} 
	}
	else {
		result = DefWindowProcA(window_handle, message, w_param, l_param);
	} 
	return result;
}

let main = fun () => void 
{
	var perf_count_freq: i64;
	QueryPerformanceFrequency(&perf_count_freq);
	
	// HACK: so that LLVM wont optimize cosf, sinf out
	// how to do that cleanly???
	if ((mm)__chkstk == 1234 || (mm)cosf == 1234 || (mm)sinf == 1234) {
		print_string("");
	}

	load_x_input();
	create_backbuffer(&backbuffer, 1244, 705);

	var class_name  = cstr("PragmaScriptWindowClass\0");
	var window_name = cstr("handmade-pragmascript\0");
	var instance = GetModuleHandleA(null);
	var window_class: WNDCLASSEX;
	window_class.cbSize = 80;
	window_class.style = (CS_HREDRAW|CS_VREDRAW);
	window_class.lpfnWndProc = (ptr)main_window_callback;
	window_class.cbClsExtra = 0;
	window_class.cbWndExtra = 0;
	window_class.hInstance = instance;
	window_class.hIcon = null;
	window_class.hCursor = null;
	window_class.hbrBackground = null;
	window_class.lpszMenuName = nullptr;
	window_class.lpszClassName = class_name;
	window_class.hIconSm = null;
	RegisterClassExA(&window_class);
	
	window.handle = CreateWindowExA(
		0,
		class_name,
		window_name,
		WS_OVERLAPPEDWINDOW|WS_VISIBLE,
		CW_USEDEFAULT,
		CW_USEDEFAULT,
		CW_USEDEFAULT,
		CW_USEDEFAULT,
		null,
		null,
		instance,
		null
	);
	window.dc = GetDC(window.handle);
	update_window_rect();


	var sound_output: sound_output;
	sound_output.samples_per_second = 48000;
	sound_output.bytes_per_chunk = 2 * (i32)(sizeof(i16)); // 2 channels
	sound_output.buffer_size = sound_output.samples_per_second * sound_output.bytes_per_chunk * 1; // 1 seccond buffer
	sound_output.latency_byte_count = sound_output.buffer_size / 15; // 66ms latency
	var sound_buffer = init_direct_sound(window.handle, sound_output.buffer_size, sound_output.samples_per_second);
	sound_output.sound_buffer = sound_buffer;
	clear_sound_buffer(&sound_output);
	var game_sample_buffer = (i16*)VirtualAlloc(nullptr, (mm)sound_output.buffer_size, MEM_COMMIT, PAGE_READWRITE);
	assert((mm)game_sample_buffer != null);
 
	var base_address = nullptr;
	if (HANDMADE_INTERAL) {
		base_address = (ptr)terabytes(2);
	} else {
		base_address = nullptr;
	}
	var game_memory = game_memory {}; 
	game_memory.permanent_size = megabytes(32);
	game_memory.transient_size = gigabytes(1);
	var total_size = game_memory.permanent_size + game_memory.transient_size;
	game_memory.permanent_data = VirtualAlloc(base_address, total_size, MEM_RESERVE|MEM_COMMIT, PAGE_READWRITE);
	assert((mm)game_memory.permanent_data != null);
	
	game_memory.transient_data = (ptr)((mm)game_memory.permanent_data + (mm)game_memory.permanent_size);
	assert((mm)game_memory.transient_data != null);

	assert(sound_buffer.vtable.Play(sound_buffer, 0, 0, DSBPLAY_LOOPING) == DS_OK);
	running = true;
	var frames = 0;
	var last_fps: f32 = 60;
	var current_counter: i64;
	var last_counter: i64;
	var last_counter_debug_stats: i64 = 0;
	QueryPerformanceCounter(&last_counter);

	var current_cycle_count: i64;
	var last_cycle_count = _rdtsc();
	var dt = 1.0 / 60.0;
	while (running) {
		var msg: MSG;
		while (PeekMessageA(&msg, null, 0, 0, PM_REMOVE) != 0) {
			if (msg.message == WM_QUIT) {
				running = false;
			}
			TranslateMessage(&msg);
			DispatchMessageA(&msg);
		}

		var game_render_target = game_render_target { };
		game_render_target.memory = backbuffer.memory;
		game_render_target.width = backbuffer.width;
		game_render_target.height = backbuffer.height;
		game_render_target.pitch = backbuffer.pitch;

		var write_cursor: i32;
		var play_cursor : i32;
		var error = sound_buffer.vtable.GetCurrentPosition(sound_buffer, &play_cursor, &write_cursor);
		assert(error == DS_OK);
		var byte_to_lock = (sound_output.sample_index * sound_output.bytes_per_chunk) % sound_output.buffer_size;
		var target_cursor = (play_cursor + sound_output.latency_byte_count) % sound_output.buffer_size;
		var bytes_to_write: i32 = 0;
		if (byte_to_lock > target_cursor) {
			bytes_to_write = sound_output.buffer_size - byte_to_lock + target_cursor;
		} else {
			bytes_to_write = target_cursor - byte_to_lock;
		}
		var game_sound_output = game_sound_output { };
		game_sound_output.samples_per_second = sound_output.samples_per_second;
		game_sound_output.sample_out = game_sample_buffer;
		game_sound_output.sample_count = bytes_to_write / sound_output.bytes_per_chunk;
		game_sound_output.samples_per_second = sound_output.samples_per_second;

		var game_time = game_time{};
		game_time.dt = dt;

		game_update_and_render(&game_memory, &game_time, &game_input, &game_render_target, &game_sound_output);

		fill_sound_buffer(&sound_output, byte_to_lock, bytes_to_write, &game_sound_output);
	
		blit_to_screen(&backbuffer);
		++frames;

		current_cycle_count = _rdtsc();
		QueryPerformanceCounter(&current_counter);
		dt = (f32)((f64)(current_counter - last_counter) / (f64)perf_count_freq); 
		if (DEBUG_DRAW_FPS && (current_counter - last_counter_debug_stats > perf_count_freq))
		{
			last_counter_debug_stats = current_counter;
			var counter_elapsed = current_counter - last_counter;
			var cycles_elapsed = current_cycle_count - last_cycle_count;
			var frame_ms = 1000.0 * (f64)counter_elapsed / (f64)perf_count_freq;
			print_string("\n");
			print_f64(frame_ms);
			print_string("ms/f   ");
			print_f64(1000.0 / frame_ms);
			last_fps = 0.5 * last_fps + (f32)(0.5 * (1000.0 / frame_ms));
			print_string("fps   ");
			print_f64((f64)cycles_elapsed / 1000000.0);
			print_string("mcycles   ");
			debug_print_f32("dt", dt);
			frames = 0;
		}
		last_counter = current_counter;
		last_cycle_count = current_cycle_count;
	}
    ExitProcess(0);
}

