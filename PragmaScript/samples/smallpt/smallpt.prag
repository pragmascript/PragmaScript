import "..\preamble.prag"
import "..\windows.prag"

// https://www.shadertoy.com/view/Xdf3zB

let Vec = struct 
(
    x: f32;
    y: f32;
    z: f32;
);
let v3 = fun (x: f32; y: f32; z: f32) => Vec {
    var result = Vec { x, y, z};
    return result;
}


var vz = Vec {0.0, 0.0, 0.0};

let add = fun (a: Vec*; b: Vec*) => Vec {
    var result = Vec { a.x + b.x, a.y + b.y, a.z + b.z};
    return result;
}
let sub = fun (a: Vec*; b: Vec*) => Vec {
    var result = Vec { a.x - b.x, a.y - b.y, a.z - b.z};
    return result;
}
let scaled = fun (a: Vec*; s: f32) => Vec {
    var result = Vec { a.x * s, a.y * s, a.z * s};
    return result;
}
let hadamard = fun (a: Vec*; b: Vec*) => Vec {
    var result = Vec { a.x * b.x, a.y * b.y, a.z * b.z};
    return result;
}
let normalize = fun (@a: Vec*) => void {
    var scl = 1.0 / Math.sqrt(x*x + y*y + z*z);
    x *= scl;
    y *= scl;
    z *= scl;
}
let normalized = fun (v: Vec*) => Vec {
    var result = *v;
    normalize(&result);
    return result;
}
let length = fun(@a: Vec*) => f32 {
    return Math.sqrt(x*x + y*y + z*z);
}
let sqr_length = fun(@a: Vec*) => f32 {
    var result = x*x + y*y + z*z;
    return result;
}

let dot = fun (a: Vec*; b: Vec*) => f32 {
    var result = a.x*b.x + a.y*b.y + a.z*b.z;
    return result;
}
let cross = fun (a: Vec*; b: Vec*) => Vec {
    var result = Vec 
    { 
        a.y * b.z - a.z * b.y, 
        a.z * b.x - a.x * b.z,
        a.x * b.y - a.y * b.x
    };
    return result;
}
let get_orthogonal = fun (v: Vec*) => Vec {
    var c0 = Vec {1.0, 0.0, 0.0};
    var c1 = Vec {0.0, 1.0, 0.0};
    var result = cross(v, &c0);
    if (sqr_length(&result) < 0.0001) {
        result = cross(v, &c1);
    }
    return result;
}

let Ray = struct 
(
    origin: Vec;
    direction: Vec;
);

let Sphere = struct 
(
    radius: f32;
    pos: Vec;
    emission: Vec;
    color: Vec;
);


let intersect_sphere_ray = fun (sphere: Sphere*; ray: Ray*; distance: f32*) => bool {
    var op = sub(&sphere.pos, &ray.origin);
    var b = dot(&op, &ray.direction);
    var det = b*b - dot(&op, &op) + sphere.radius * sphere.radius;

    if (det < 0.0) {
        return false;
    } else {
        det = Math.sqrt(det);
    }
    var eps = 0.02;
    var t = b - det;
    if (t > eps) {
        *distance = t;
        return true;
    } else {
        t = b + det;
        if (t > eps) {
            *distance = t;
            return true;
        } else {
            return false;
        }
    }
    assert(false);
    return false;
}

let clamp_01 = fun (x: f32) => f32 {
    if (x < 0.0) {
        return 0.0;
    } elif (x > 1.0) {
        return 1.0;
    }
    return x;
}

let to_int = fun (x: f32) => i32 {
    var m = Math.pow(clamp_01(x), 1.0 / 2.2) * 255.0 + 0.5;
    return (i32)(Math.trunc(m));
}

let intersect = fun(ray: Ray*; t: f32*; obj: Sphere**) => bool {
    let max = 10000000000.0;
    *t = max;
    *obj = (Sphere*)nullptr;
    
    var d: f32;
    var s = spheres;
    for (var i = 0; i < sphere_count; ++i) {
        if (intersect_sphere_ray(s, ray, &d) && d < *t) {
            *t = d;
            *obj = s;
        }
        s++;
    }
    return *t != max;
}

let radiance = fun(ray: Ray*; depth: i32) => Vec {
    var t: f32;
    var id = 0;
    var obj: Sphere*;
    if (!intersect(ray, &t, &obj)) {
        return vz;
    }
    
    var color = obj.color;
    
    if (depth > 5) {
        // var nax_refl = Math.max(color.x, Math.max(color.y, color.z));
        // if (Random.rand_f32() > nax_refl) {
        //     return obj.emission;
        // } else {
        //     color = scaled(&color, 1.0 / nax_refl);
        // }
        return color;
    }
    

    var delta = scaled(&ray.direction, t);
    var x = add(&ray.origin, &delta);
    var n = sub(&x, &obj.pos);
    normalize(&n);

    // return n;

    var nl = n;
    if (dot(&n, &ray.direction) >= 0)  {
        nl = scaled(&n, -1);
    }

    
    var r1 = Random.rand_f32(0, Math.tau_32);
    var r2 = Random.rand_f32();
    var r2s = Math.sqrt(r2);
    var w = nl;
    var u = get_orthogonal(&w);
    normalize(&u);
    var v = cross(&w, &u);
    
    // get random vec on hemisphere
    var d1 = scaled(&u, Math.cos(r1) * r2s);
    var d2 = scaled(&v, Math.sin(r1) * r2s);
    var d3 = scaled(&w, Math.sqrt(1 - r2));
    var d = add(&d1, &d2);
    d = add(&d, &d3);

    var result = obj.emission;
    var random_ray = Ray{x, d};
    var recursive = radiance(&random_ray, depth + 1);
    recursive = hadamard(&color, &recursive);
    result = add(&result, &recursive);

    return result;
}



let sphere_count = 9;
var spheres: Sphere*;

let create_spheres = fun() => void {
    spheres = (Sphere*)Windows.VirtualAlloc(nullptr, (mm)(size_of(Sphere) * (mm)sphere_count), Windows.MEM_COMMIT, Windows.PAGE_READWRITE);
    var s = spheres;
    
    // crashes the compiler
    // var s = Sphere { big, Vec { big + 1  , 40.8, 81.6 }, vz            , Vec {0.75, 0.25, 0.25} };

    let big = 10000.0;
    // 0
    s.radius   = big;
    s.pos      = v3(big + 1, 40.8, 81.6);
    s.emission = vz;
    s.color    = v3(0.75, 0.25, 0.25);
    s++;
    // 1
    s.radius   = big;
    s.pos      = v3(-big + 99, 40.8, 81.6);
    s.emission = vz;
    s.color    = v3(0.25, 0.25, 0.75);
    s++;
    // 2
    s.radius   = big;
    s.pos      = v3(50, 40.8, big);
    s.emission = vz;
    s.color    = v3(0.75, 0.75, 0.75);
    s++;
    // 3
    s.radius   = big;
    s.pos      = v3(50, 40.8, -big + 170);
    s.emission = vz;
    s.color    = vz;
    s++;
    // 4
    s.radius   = big;
    s.pos      = v3(50, big, 81.6);
    s.emission = vz;
    s.color    = v3(0.75, 0.75, 0.75);
    s++;
    // 5
    s.radius   = big;
    s.pos      = v3(50, -big + 81.6, 81.6);
    s.emission = vz;
    s.color    = v3(0.75, 0.75, 0.75);
    s++;
    // 6
    s.radius   = 16.5;
    s.pos      = v3(27, 16.5, 47);
    s.emission = vz;
    s.color    = v3(0.99, 0.99, 0.99);
    s++;
    // 7
    s.radius   = 16.5;
    s.pos      = v3(73, 16.5, 78);
    s.emission = vz;
    s.color    = v3(0.99, 0.25, 0.25);
    s++;
    // 8
    s.radius   = 600;
    s.pos      = v3(50, 681.6 - 0.27, 81.6);
    s.emission = v3(12.0, 12.0, 12.0);
    s.color    = vz;
}

let platform_write_file = fun (name: string; data: ptr; size_in_bytes: i32) => bool
{
    var handle = Windows.CreateFileA(cstr(name), Windows.GENERIC_WRITE, 0, nullptr, Windows.CREATE_ALWAYS, 0, 0);
    if (handle == null) {
        Windows.CloseHandle(handle);
        return false;
    }
    var bytes_written: i32 = 0;
    var result = Windows.WriteFile(handle, data, size_in_bytes, &bytes_written, nullptr);
    if (result != 0) {
        Windows.CloseHandle(handle);
        assert(size_in_bytes == bytes_written);
        return true;
    } else {
        Windows.CloseHandle(handle);
        return false;
    }

    Windows.CloseHandle(handle);
    return false;
}


let Tile = struct 
(
    cam: Ray;
    cam_x: Vec;
    cam_y: Vec;
    width: i32;
    height: i32;

    sx: i32;
    sy: i32;
    samples: i32;
    start: Vec*;
    tile_size: i32;
);

let render_tile = fun (@tile: Tile*) => void {
    var p = start;    
    for (var y = 0; y < tile_size; ++y) {
        var pixels = p;
        for (var x = 0; x < tile_size; ++x) {
            var sample_radiance = vz;
            for (var s = 0; s < samples; ++s) {
                var r1 = Random.rand_f32(0, 2.0);
                var dx: f32;
                if (r1 < 1.0) {
                    dx = Math.sqrt(r1) - 1;
                } else {
                    dx = 1.0 - Math.sqrt(2 - r1);
                }
                var r2 = Random.rand_f32(0, 2.0);
                var dy: f32;
                if (r2 < 1.0) {
                    dy = Math.sqrt(r2) - 1;
                } else {
                    dy = 1.0 - Math.sqrt(2 - r2);
                }
                
                var px = ((f32)sx + (f32)x + 0.5*dx) / (f32)width;
                var py = ((f32)sy + (f32)y + 0.5*dy) / (f32)height;

                var scx = scaled(&cam_x, px - 0.5);
                var scy = scaled(&cam_y, py - 0.5);
                var d = cam.direction;
                d = add(&d, &scx);
                d = add(&d, &scy);
                normalize(&d);

                var rp = cam.origin;
                var push_d = scaled(&d, 140);
                rp = add(&rp, &push_d);
                var sample_ray = Ray{rp, d};
                
                var r = radiance(&sample_ray, 0);
                sample_radiance = add(&sample_radiance, &r);
            }
            var sr = scaled(&sample_radiance, 1.0 / (f32)samples);
           
            *pixels++ = sr;
        }
        p += width;
    }
}



let RenderJob = struct
(
    thread_idx: i32;
    thread_count: i32;
    tiles: Tile[];
    completed: bool;
);

let do_render_job = fun (job: RenderJob*) => i32 {
    print_string("wooooo thread! \n");
}





[
	"compile.output": "smallpt.exe",
	"compile.entry" : "true",
	"compile.debug" : "false",
 	"compile.opt"   : "3",
 	"compile.run"   : "false",
 	"compile.libs"  : "kernel32.lib, libopenlibm.lib",
	"compile.path"  : "C:\Program Files (x86)\Windows Kits\10\Lib\10.0.14393.0\um\x64, lib"
]
let main = fun () => void 
{

    // for (var i = 0 ; i < 12; ++i) {
    //     debug_print_f32("rand_f32()", Random.rand_f32());
    // }
    // return;

    

    create_spheres();

    let samples = 8;
    let tile_size = 128;
    let x_tiles = 8;
    let y_tiles = 6;
    let width  = x_tiles * tile_size;
    let height = y_tiles * tile_size;
    var pixel_data = (Vec*)Windows.VirtualAlloc(nullptr, size_of(Vec) * (mm)width * (mm)height, Windows.MEM_COMMIT, Windows.PAGE_READWRITE);

    var cam: Ray;
    cam.origin = v3(50,52,295.6);
    cam.direction = v3(0, -0.042612, -1);
    normalize(&cam.direction);

    let fov = 0.5135;
    var cam_x = v3((f32)width * fov / (f32)height, 0, 0);
    var cam_y = cross(&cam_x, &cam.direction);
    normalize(&cam_y);
    cam_y = scaled(&cam_y, fov);


    // create tiles
    var tile_count = x_tiles * y_tiles;
    var tiles: Tile[];
    tiles.data = (Tile*)Windows.VirtualAlloc(nullptr, size_of(Tile) * (mm)tile_count, Windows.MEM_COMMIT, Windows.PAGE_READWRITE);
    tiles.length = tile_count;
    var idx = 0;
    for (var j = 0; j < y_tiles; ++j) {
        for (var i = 0; i < x_tiles; ++i) {
            var t = &tiles[idx++];
            t.sx = tile_size * i;
            t.sy = tile_size * j;
            t.start = pixel_data + (t.sy * width + t.sx);
            t.tile_size = tile_size;
            t.width = width;
            t.height = height;
            t.cam = cam;
            t.cam_x = cam_x;
            t.cam_y = cam_y;
            t.samples = samples;
        }
    }

    // create jobs
    let thread_count = 8;
    var jobs: RenderJob[];
    jobs.data = (RenderJob*)Windows.VirtualAlloc(nullptr, size_of(RenderJob) * (mm)thread_count, Windows.MEM_COMMIT, Windows.PAGE_READWRITE);
    jobs.length = thread_count;
    for (var i = 0; i < jobs.length; ++i) {
        var j: RenderJob;
        j.thread_idx = i;
        j.thread_count = thread_count;
        j.tiles = tiles;
    }
    for (var i = 1; i < jobs.length; ++i) {
        var job = &jobs[i];
        var thread_id: mm;
        var thread_handle = Windows.CreateThread(nullptr, null, (Windows.ThreadProc)do_render_job, (ptr)job, 0, &thread_id);
    }
    do_render_job(&jobs[0]);

    //todo(pragma): wait until all jobs finished. volatile!

    Windows.Sleep(1000);
    Windows.ExitProcess(0);


    for (var t_idx = 0; t_idx < tiles.length; ++t_idx) {
        render_tile(&tiles[t_idx]);
        var pc = 100.0 * ((f32)t_idx + 1) / (f32)tiles.length;
        print_f32(pc, 2);
        print_string("%\n");

    }
    print_string("finished tracing.\n");

    var pif = pif_data;
    var pixels = pixel_data;
    *pif++ = 3141592654;
    *pif++ = width;
    *pif++ = height;
    for (var y = 0; y < height; ++y) {
        for (var x = 0; x < width; ++x) {
            var v = *pixels;
            var r = to_int(v.x);
            var g = to_int(v.y);
            var b = to_int(v.z);
            *pif++ = r;
            *pif++ = g;
            *pif++ = b;
            pixels++;
        }
    }
    platform_write_file("image.pif\0", (ptr)pif_data, (i32)pif_size);
    var pif_size = 3*size_of(i32) * (mm)width * (mm)height + 3*size_of(i32);
    var pif_data = (i32*)Windows.VirtualAlloc(nullptr, pif_size, Windows.MEM_COMMIT, Windows.PAGE_READWRITE);

    Windows.ExitProcess(0);
}
